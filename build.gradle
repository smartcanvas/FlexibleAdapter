// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
	//Author
	developerId = "davideas"
	developerName = "Davide Steduto"
	developerEmail = "dave.dna@gmail.com"

	//Library
	libraryCode = 17
	libraryVersion = "5.0.0-b7"
	libraryDate = " built on " + getDate()
	libraryDescription = "1 Adapter for SelectionMode, Undo, ViewHolders, Filter, FastScroller, Animations, Sticky Headers, Expandable, Draggable, Swipeable, EndlessScroll :-)"
	libraryName = "FlexibleAdapter"
	packageExt = "aar"

	//Library Repository
	bintrayRepo = "maven"
	bintrayName = "flexible-adapter"
	publishedGroupId = "eu.davidea"
	artifact = bintrayName
	siteUrl = "https://github.com/davideas/FlexibleAdapter"
	gitUrl = "https://github.com/davideas/FlexibleAdapter.git"

	//Support and Build tools version
	minSdk = 14
	targetSdk = 24
	buildTools = "23.0.3"
	supportLib = "24.0.0"

	//Support Libraries dependencies
	supportDependencies = [
		design       : "com.android.support:design:${supportLib}",
		recyclerview : "com.android.support:recyclerview-v7:${supportLib}",
		cardview     : "com.android.support:cardview-v7:${supportLib}",
		appcompat    : "com.android.support:appcompat-v7:${supportLib}",
		customtabs   : "com.android.support:customtabs:${supportLib}",
		support      : "com.android.support:support-v13:${supportLib}",
		annotations  : "com.android.support:support-annotations:${supportLib}"
	]

	licenseName = "The Apache Software License, Version 2.0"
	licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
	allLicenses = ["Apache-2.0"]
	licenseDist = "repo"
}

def getDate() {
	def date = new Date()
	return date.format('yyyy.MM.dd')
}

//Avoid Javadoc Lint failures when using Java8
if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

buildscript {
	repositories {
		jcenter()
		maven {url = "https://oss.sonatype.org/content/repositories/snapshots/" }
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.2'
		classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
		classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
		classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

allprojects {
	repositories {
		jcenter()
		maven {url = "http://dl.bintray.com/davideas/maven" }
		maven {url = "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
}